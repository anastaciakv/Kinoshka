apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0 rc2'
    defaultConfig {
        applicationId "de.proximity.kinoshka"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        buildConfigField "String", "THE_MOVIE_DB_API_KEY", "\"${API_KEY}\""
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }
}
jacoco {
    toolVersion = "0.7.4+"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"

    //DB - Room
    compile "android.arch.persistence.room:runtime:$rootProject.archVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.archVersion"

    //Picasso
    compile "com.squareup.picasso:picasso:$rootProject.picassoVersion"

    //Retrofit
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.loggingInterceptorVersion"

    // Dagger dependencies
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    compile "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    compile "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"

    //Parcelable
    compile "org.parceler:parceler-api:$rootProject.parcelerVersion"
    annotationProcessor "org.parceler:parceler:$rootProject.parcelerVersion"

    //Logs
    compile "com.jakewharton.timber:timber:$rootProject.timberVersion"

    //Lifecycle Awareness
    compile "android.arch.lifecycle:runtime:$rootProject.archVersion"
    compile "android.arch.lifecycle:extensions:$rootProject.archVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$rootProject.archVersion"

    //unit tests; mocking objects
    testCompile "junit:junit:$rootProject.junitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testCompile("android.arch.core:core-testing:$rootProject.archVersion", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })

    //ui tests; mocking objects
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile("android.arch.core:core-testing:$rootProject.archVersion", {
    })

}

task fullCoverageReport(type: JacocoReport) {
//    dependsOn 'createDebugCoverageReport'
    dependsOn 'connectedAndroidTest'
    dependsOn 'testDebugUnitTest'
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                      '**/*Test*.*', 'android/**/*.*',
                      '**/*_MembersInjector.class', '**/*_Impl*', '**/*Binding*',
                      '**/Dagger*Component*.class', '**/*ViewModelFactory.class',
                      '**/Dagger*Component$Builder.class', '**/*$$Parcelable.class',
                      '**/*_*Factory.class', '**/MoviesDb.class',
                      '**/*ComponentImpl.class', '**/BR.class', '**/*_Contribute*.class',
                      '**/*SubComponentBuilder.class']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])
}